# Maintainer: Christopher Reimer <vdr4arch[at]creimer[dot]net>
pkgname=v4l-dvb-saa716x
pkgver=20130224
_hgver=cfa4b4faab67
_extramodules=extramodules-3.10-ARCH
url='http://powarman.dyndns.org/hgwebdir.cgi/v4l-dvb-saa716x'
pkgrel=6
pkgdesc="All interesting modules of powarman's v4l-dvb-saa716x repository"
arch=('x86_64' 'i686')
license=('GPL2')
depends=('linux>=3.10' 'linux<3.11')
makedepends=('linux-headers>=3.10' 'linux-headers<3.11' 'mercurial')
install="${pkgname}.install"
replaces=('saa716x')
conflicts=('saa716x')
source=("hg+http://linuxtv.org/hg/~endriss/mirror-saa716x#revision=$_hgver"
        'compat.diff')
md5sums=('SKIP'
         'a99714ecdaaa849f7c3d4d93ebf1c81d')

pkgver() {
  cd "$srcdir/mirror-saa716x"
  hg log -l1 --template "{shortdate(date)}" | sed 's/-//g'
}

prepare() {
  cd "$srcdir/mirror-saa716x"
  patch -p1 -i "$srcdir/compat.diff"
}

build() {
  _kernver="$(cat /lib/modules/$_extramodules/version)"

  cd "$srcdir/mirror-saa716x/linux/drivers/media/common/saa716x"
  make -C /lib/modules/$_kernver/build M=$(pwd) CONFIG_SAA716X_CORE=m CONFIG_DVB_SAA716X_BUDGET=m \
    CONFIG_DVB_SAA716X_HYBRID=m CONFIG_DVB_SAA716X_FF=m modules
}


package() {
  cd "$srcdir"

  mkdir -p "$pkgdir/usr/lib/modules/$_extramodules"
  find -name *.ko -exec cp {} "$pkgdir/usr/lib/modules/$_extramodules" \;
  gzip -9 "$pkgdir/usr/lib/modules/$_extramodules/"*
}
